// For debugging:
// '-agentlib:jdwp=transport=dt_socket,address=9009,server=y,suspend=y'

plugins {
    id "java"
    id "maven"
    id "maven-publish"
    id "eclipse"
    id "idea"
    id "com.jfrog.bintray" version "1.2"
	id "me.champeau.gradle.antlr4" version "0.1"
	id "jacoco"
}

group = "ch.liquidmind"
version = "0.5"

jar {
    from ("./LICENSE") {
        into "META-INF"
    }
}

repositories {
    mavenCentral()
	maven {
		url "http://dl.bintray.com/deflector/maven"
	}
}

sourceSets {
	main {
		resources {
            srcDir "src/main/resources"
            exclude "**/*.g4"
		}
	}
    testModel {
        java {
            srcDir "src/test/java"
            include "ch/liquidmind/inflection/test/model/*"
        }
        resources {
        	srcDir "src/test/resources"
        }
    }
    test {
    	java {
	    	srcDirs "src/test/java", "build/generated/inflection"
    	}
        resources {
        	srcDirs "src/test/resources", "build/resources/testModel"
        }
    }
}

dependencies {
	compile "__commons-io:commons-io:2.4"
	compile "__org.antlr:antlr4:4.5"
	compile "com.google.guava:guava:19.0-rc2"
	compile "junit:junit:4.12"
	compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.13'
}

// Antlr
antlr4 {
	source=file("src/main/resources/ch/liquidmind/inflection/grammar")
	output=file("$buildDir/generated/antlr/ch/liquidmind/inflection/grammar")
}
compileJava.dependsOn antlr4
sourceSets.main.java.srcDirs += "$buildDir/generated/antlr"

// Inflect test model
task( compileTestTaxonomiesInflection, dependsOn: compileTestModelJava, type: JavaExec ) {
	main = "ch.liquidmind.inflection.compiler.InflectionCompiler"
	classpath = sourceSets.main.runtimeClasspath + sourceSets.testModel.runtimeClasspath
	jvmArgs '-ea'
	args "${buildDir}/resources/testModel", "BOOTSTRAP", 
	"src/test/resources/ch/liquidmind/inflection/test/model/TestModel.inflect", 
	"src/test/resources/ch/liquidmind/inflection/test/blackbox/BlackboxTestModel.inflect", 
	"src/test/resources/ch/liquidmind/inflection/model/external/TaxonomyTest.inflect",
	"src/test/resources/ch/liquidmind/inflection/model/external/ViewTest.inflect",
	"src/test/resources/ch/liquidmind/inflection/model/external/MemberTest.inflect",
	"src/test/resources/ch/liquidmind/inflection/proxy/ProxyGeneratorTestModel.inflect"
}

compileTestTaxonomiesInflection.doFirst {
	delete fileTree( dir: "build/resources/testModel" , includes: [ "**/*.tax" ] )
}

// Proxify test taxonomies
task( proxifyTestTaxonomies, dependsOn: compileTestTaxonomiesInflection, type: JavaExec ) {
	main = "ch.liquidmind.inflection.proxy.ProxyGenerator"
	classpath = sourceSets.main.runtimeClasspath + sourceSets.testModel.runtimeClasspath
	jvmArgs '-ea'
	args "${buildDir}/generated/inflection",
	"ch.liquidmind.inflection.test.model.FullTaxonomy",
	"ch.liquidmind.inflection.test.model.UseCase1",
	"ch.liquidmind.inflection.test.model.UseCase2",
	"ch.liquidmind.inflection.test.model.UseCase3",
	"ch.liquidmind.inflection.test.model.UseCase4",
	"ch.liquidmind.inflection.test.blackbox.BlackboxTestTaxonomy",
	"ch.liquidmind.inflection.test.blackbox.BlackboxTestCalculatedTaxonomy",
	"ch.liquidmind.inflection.model.external.TaxonomyTestTaxonomy",
	"ch.liquidmind.inflection.model.external.ViewTestTaxonomy",
	"ch.liquidmind.inflection.model.external.MemberTestTaxonomy"
}

// Compile test
compileTestJava.dependsOn proxifyTestTaxonomies

test {
	// Enable this to see test output:
	testLogging.showStandardStreams = true
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
check.dependsOn jacocoTestReport

// Sources JAR
task sourceJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
}

// Javadocs JAR
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }

            pom.withXml {
                def root = asNode()
                root.appendNode('name',
                                    'ch.liquidmind:inflection')
                root.appendNode('description',
                                    'Turns checked exceptions into runtime exceptions by generating wrapper classes for arbitrary APIs in Java.')
                root.appendNode('url',
                                    'http://www.liquidmind.ch/')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')

                def scm = root.appendNode('scm')
                scm.appendNode('connection', 'scm:git:git@github.com:liquid-mind/inflection.git')
                scm.appendNode('developerConnection', 'scm:git:git@github.com:liquid-mind/inflection.git')
                scm.appendNode('url', 'git@github.com:liquid-mind/inflection.git')

                def developer = root.appendNode('developers').appendNode('developer')
                developer.appendNode('name', 'John Brush')
                developer.appendNode('email', 'brsh@zhaw.ch')
                developer.appendNode('organization', 'Liquidmind')
                developer.appendNode('organizationUrl', 'http://www.liquidmind.ch')
            }
        }
    }
}

bintray {
        publications = ['mavenJava']
        dryRun = false
        publish = false
        user=BINTRAY_USER
        key=BINTRAY_API_KEY
        pkg {
            repo = 'maven'
            name = 'inflection'
			licenses = ['Apache-2.0']
			vcsUrl = 'https://github.com/liquid-mind/inflection.git'
            version {
                gpg {
                    sign = true
                    passphrase=BINTRAY_GPG_PASS
                }
            }
        }
}
